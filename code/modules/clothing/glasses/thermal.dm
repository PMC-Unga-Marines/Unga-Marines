/obj/item/clothing/glasses/thermal
	name = "Optical Thermal Scanner"
	desc = "Thermals in the shape of glasses."
	icon_state = "thermal"
	item_state = "glasses"
	vision_flags = SEE_MOBS
	lighting_alpha = LIGHTING_PLANE_ALPHA_INVISIBLE
	eye_protection = -1
	deactive_state = "goggles_off"

/obj/item/clothing/glasses/thermal/emp_act(severity)
	if(ishuman(loc))
		var/mob/living/carbon/human/M = src.loc
		to_chat(M, span_warning("The Optical Thermal Scanner overloads and blinds you!"))
		if(M.glasses == src)
			M.blind_eyes(3)
			M.blur_eyes(5)
	return ..()

/obj/item/clothing/glasses/thermal/yautja
	name = "bio-mask thermal"
	desc = "A vision overlay generated by the Bio-Mask. Used to sense the heat of prey."
	icon = 'icons/obj/hunter/pred_gear.dmi'
	icon_state = "visor_thermal"
	item_state = "visor_thermal"
	item_icons = list(
		slot_glasses_str = 'icons/mob/hunter/pred_gear.dmi'
	)

/obj/item/clothing/glasses/thermal/yautja/Initialize()
	. = ..()
	ADD_TRAIT(src, TRAIT_NODROP, TRAIT_GENERIC)

/obj/item/clothing/glasses/thermal/yautja/dropped(mob/living/carbon/human/user)
	if(istype(user) && user.glasses == src)
		user.clear_fullscreen("machine", 5)
	return ..()

/obj/item/clothing/glasses/thermal/yautja/equipped(mob/living/carbon/human/user, slot)
	if(slot == SLOT_GLASSES)
		user.overlay_fullscreen("machine", /atom/movable/screen/fullscreen/machine/pred)
	return ..()

/obj/item/clothing/glasses/thermal/yautja/unequipped(mob/living/carbon/human/user, slot)
	if(slot == SLOT_GLASSES)
		user.clear_fullscreen("machine", 5)
	return ..()
